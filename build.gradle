buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin-multiplatform'

group 'se.kth.somabits'
version '0.0.1'

repositories {
    jcenter()
    mavenLocal()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url = uri("https://dl.bintray.com/kotlin/kotlin-js-wrappers") }
    maven {
        url = uri("https://dl.bintray.com/gbaldeck/kotlin")
        metadataSources {
            mavenPom()
            artifact()
        }
    }
    maven { url = uri("https://dl.bintray.com/rjaros/kotlin") }

}

kotlin {
    js('frontend') {
        browser {
        }
    }
    jvm('backend') {
        compilations.main {
            tasks.getByName(processResourcesTaskName) {
                dependsOn(frontendBrowserProductionWebpack)
                from(frontendBrowserProductionWebpack.outputPath)
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        backendMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
                implementation "io.ktor:ktor-server-core:$ktor_version"
                implementation "io.ktor:ktor-websockets:$ktor_version"
                implementation "io.ktor:ktor-network:$ktor_version"
                implementation "io.ktor:ktor-serialization:$ktor_version"
                implementation "io.ktor:ktor-gson:$ktor_version"
                implementation "org.jmdns:jmdns:3.5.5"
                implementation "com.illposed.osc:javaosc-core:0.7"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
            }
        }
        backendTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        frontendMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation(npm("abort-controller", "3.0.0"))
                implementation(npm("text-encoding", "0.7.0"))

                implementation "io.ktor:ktor-client-js:$ktor_version"
                implementation "io.ktor:ktor-client-websockets:$ktor_version"
                implementation("org.jetbrains.kotlinx:kotlinx-html-js:0.7.1")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$kotlinx_version")
            }
        }
        frontendTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        all {
            languageSettings.enableLanguageFeature("InlineClasses")
        }
    }
}

task run(type: JavaExec, dependsOn: [backendJar]) {
    main = "se.kth.somabits.backend.ApplicationKt"
    classpath(configurations.backendRuntimeClasspath, backendJar)
    args = []
    jvmArgs = ["-Djava.net.preferIPv4Stack=true"]
}
